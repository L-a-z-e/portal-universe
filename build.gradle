plugins {
    id 'org.springframework.boot' version '3.5.5' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

subprojects {
    // Apply truly universal settings to all subprojects
    group = 'com.portal.universe'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

// Apply Java-specific configurations only to projects that have the Java plugin
allprojects { project ->
    project.plugins.withType(JavaPlugin) { 
        project.apply plugin: 'io.spring.dependency-management'

        project.sourceCompatibility = '17'

        project.dependencyManagement {
            imports {
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
            }
        }

        project.dependencies {
            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }

        project.test {
            useJUnitPlatform()
        }
    }
}