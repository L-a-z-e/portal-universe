plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.portal.universe'
version = '0.0.1-SNAPSHOT'
description = 'shopping-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    // Spring Cloud의 버전 관리를 위한 변수
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    // =======================================
    // Internal Libraries
    // =======================================
    implementation project(':services:common-library') // 공통 예외 처리, 응답 형식 등

    // =======================================
    // Spring Cloud & Microservice Core
    // =======================================
    implementation 'org.springframework.cloud:spring-cloud-starter-config' // Externalized Configuration
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client' // Service Discovery
    // 다른 마이크로서비스의 API를 호출하기 위한 선언적 REST 클라이언트
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // =======================================
    // Web & Data
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-web' // Spring Web (MVC)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa' // Spring Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-validation' // Bean Validation
    runtimeOnly 'com.mysql:mysql-connector-j' // MySQL Driver

    // =======================================
    // Database Migration
    // =======================================
    implementation 'org.flywaydb:flyway-core' // Database migration
    implementation 'org.flywaydb:flyway-mysql' // Flyway MySQL support

    // =======================================
    // Messaging (Kafka)
    // =======================================
    implementation 'org.springframework.kafka:spring-kafka' // Kafka producer/consumer

    // =======================================
    // Redis & Distributed Lock
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-data-redis' // Redis client
    implementation 'org.redisson:redisson-spring-boot-starter:3.27.0' // Distributed lock

    // =======================================
    // Elasticsearch
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch' // Elasticsearch client
    implementation 'co.elastic.clients:elasticsearch-java:8.11.0' // Elasticsearch Java client

    // =======================================
    // SSE (Server-Sent Events)
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-webflux' // For Flux/SSE support

    // =======================================
    // Security (OAuth2 Resource Server)
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server' // JWT 토큰 검증
    implementation 'org.springframework.security:spring-security-oauth2-jose' // JOSE (Javascript Object Signing and Encryption)

    // =======================================
    // Observability (Monitoring & Tracing)
    // =======================================
    implementation 'org.springframework.boot:spring-boot-starter-actuator' // Health Check, Metrics
    implementation 'io.micrometer:micrometer-registry-prometheus' // Prometheus-compatible metrics
    implementation 'io.micrometer:micrometer-tracing-bridge-brave' // Distributed Tracing (Brave)
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave' // Tracing data reporter for Zipkin

    // =======================================
    // Development Tools
    // =======================================
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // =======================================
    // Document
    // =======================================
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    // =======================================
    // Testing
    // =======================================
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test' // Kafka testing
    testImplementation 'org.testcontainers:junit-jupiter' // Integration tests with Docker containers
    testImplementation 'org.testcontainers:mysql' // MySQL container for testing
    testImplementation 'org.testcontainers:kafka' // Kafka container for testing
    testImplementation 'com.redis:testcontainers-redis:2.2.2' // Redis container for testing
    testImplementation 'org.testcontainers:elasticsearch' // Elasticsearch container for testing
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // =======================================
    // Etc.
    // =======================================
    // macOS aarch64 환경에서 Netty DNS 문제 해결을 위한 네이티브 의존성
    implementation 'io.netty:netty-resolver-dns-native-macos:4.1.94.Final:osx-aarch_64'
}

// Spring Cloud 종속성 버전 관리를 위한 BOM(Bill of Materials)
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}